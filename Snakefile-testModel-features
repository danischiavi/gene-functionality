###########################################################################################################################
# EXTRACTING FEATURES
###########################################################################################################################

##########
## SET UP 
##########

configfile: "config.yaml"

all_rna = [
	"testModel-protein-exon2", 
	"testModel-protein-exon3", 
	"testModel-lncrna-exon1", 
	"testModel-lncrna-exon2", 
	"testModel-short-ncrna" 
]

	
###################
## RUN ALL FEATURES 
###################
rule all_features:
	input:
		expand("data/{sample}-dataset-features.csv", sample=all_rna)


#############
## Intrinsic features 
#############
# GC percentage: divide number of guanine and cytosine nucleotides by total sequence length
#
# Sequence Low complexity Density: Find regions of low sequence complexity with dustmasker (XXX reference and version!). 
# Density is calculated as the ratio between nucleotides in the low complexity regions and total nucleotides on sequence
			  				
rule run_intrinsic_seq_features:
	input:
		"data/datasets/testModel/{sample}-dataset.csv",
		"data/datasets/testModel/{sample}-seq.fa"
	
	output:
		"data/intrinsic/{sample}-intrinsic.csv"

	shell:
		"scripts/B0-intrinsic-seq-features.sh {input} > {output}"


###############
## Conservation 
###############
# The maximum phyloP score for each sequence is extracted from its corresponding bigWig files using ```bigWigSummary``` (Haeussler et al., 2019). 
# # Parameters used for running bigWigSummary
# -type=max reports the maximum score
# $chr $start $end are the chromosome coordiantes for the sequence extracted from the input dataset (initial_data)
# 1 causes the score across the whole sequence to be reported

rule run_seq_conservation_features:
	input:
		initial_data="data/datasets/testModel/{sample}-dataset.csv",
		bigWigSummary_exe=config["bigWigSummary_exe"],
		_100w_phyloP_bw=config["_100w_phyloP_bw"],
		zoonomia_phyloP_bw=config["zoonomia_phyloP_bw"] 

	output:
		"data/conservation/{sample}-conservation.csv"
	  
	shell:
		"scripts/B1-seq-conservation-features.sh {input} > {output}"


################
## Transcriptome
################
# Read counts were calculated using ```samtools view -c``` and maximum read depth was calculated using ```samtools depth -r``` (Li et al., 2009).

rule run_transcriptome:
	input:
		"data/datasets/testModel/{sample}-dataset.csv", 
		encode_folder=config["encode_folder"]

	output:
		"data/transcriptome/{sample}-transcriptome.csv"
 
	shell:
		"scripts/B2-transcriptome-expression-features.sh {input} > {output}"


##################		
## Genomic repeats 
##################
# Parameters used to run mmseqs
# Distances to the nearest repetitive elements were calculated from a file of non-redundant hits of repetitive DNA elements in the human genome obtained from Dfam v3.1 (Hubley et al., 2016). Once this was converted to bed file format, the distance to the nearest hit was calculated using ```bedtools closest```  (Quinlan, 2014). 
# # Parameters used for running bedtools:
# bedtools closest reports the either overlapping or the nearest sequence in input 
# -a $input_bed is the sequences of interest, converted to a bed file
# -b dfam-hg38-nrph.bed is the Dfam non-redundent repetitive DNA elements
# -io ignores overlaps, as some functional ncRNAs are considered repetitive DNA elements
# -D ref reports distance with respect to the reference genome
# -iu ignores upstream to report closest downstream element
# -id ignores downstream to report closest upstream element


rule run_genomic_repeat:
	input:
		initial_data="data/datasets/testModel/{sample}-dataset.csv",
		initial_fasta="data/datasets/testModel/{sample}-seq.fa", 
		human_genome_mmseqs=config["human_genome_mmseqs"],
		dfam_hits=config["dfam_hits"]   

	output:
		"data/repeats/{sample}-repeats.csv"

	shell:
		"scripts/B3-genomic-repeat-associated-features.sh {input} > {output}"


###########################		
## Protein and RNA specific 
###########################
rule run_protein_and_rna_specific_features:
	input:
		initial_data="data/datasets/testModel/{sample}-dataset.csv",
		initial_fasta="data/datasets/testModel/{sample}-seq.fa",
		interaction_database=config["interaction_database"],
		bigBedToBed_exe=config["bigBedToBed_exe"]
		
		
	output:
		"data/specific/{sample}-specific.csv"

	shell:
		"scripts/B4-protein-and-rna-specific-features.sh {input} > {output}"


#######################		
## Population variation
#######################
# SNP frequencies, which were used to caluculate the average minor allele frequency, were calculated using ```vcftools``` (Danecek et al., 2011). All chromosome SNP data from the Genome Aggregation Database (gnomAD) v3 was downloaded as a local copy, with the 
# SNPs for each region extracted using ```tabix -f -h``` (Karczewski et al. 2020; Li et al., 2009).

rule run_population_variation:
	input:
		initial_data="data/datasets/testModel/{sample}-dataset.csv",
		gnomad_directory=config["gnomad_directory"]

	output:
		"data/population/{sample}-population.csv"

	shell:
		"scripts/B5-population-variation-features.sh {input} > {output}"


########################		
## Epigenetic Features  
########################
rule run_epigenetic_features:
	input:
		initial_data="data/datasets/testModel/{sample}-dataset.csv"   # include the path of tha databases as an input 

	output:
		"data/epigenetic/{sample}-epigenetic.csv"
		
	shell:
		"scripts/B6-epigenetic-features.sh {input} > {output}"


#### FORMAT FINAL FEATURES DATASET #### 
rule concatenate_features:
	input:
		"data/datasets/testModel/{sample}-dataset.csv",
		"data/intrinsic/{sample}-intrinsic.csv",
		"data/conservation/{sample}-conservation.csv",
		"data/transcriptome/{sample}-transcriptome.csv",
		"data/repeats/{sample}-repeats.csv",
		"data/specific/{sample}-specific.csv", 
		"data/population/{sample}-population.csv",
		"data/epigenetic/{sample}-epigenetic.csv"
		
	output:
		"data/{sample}-dataset-features.csv"

	shell:
		"paste -d',' {input} > {output}"
	